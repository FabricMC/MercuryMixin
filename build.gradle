plugins {
    id 'java-library'
    id 'maven-publish'

    id 'org.cadixdev.licenser' version '0.6.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

group = 'net.fabricmc'
archivesBaseName = project.name.toLowerCase()
version = '0.1.0'

repositories {
    mavenCentral()
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
}

dependencies {
    api "net.fabricmc:mercury:$mercuryVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
}

test {
    useJUnitPlatform()
}

license {
    header = project.file('HEADER.txt')

    exclude 'org/cadixdev/mercury/mixin/annotation/AccessorName.java'
    exclude 'org/cadixdev/mercury/mixin/annotation/DescData.java'
}

processResources {
    from 'LICENSE.txt'
}

def ENV = System.getenv()

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java

            pom {
                name = project.name
                description = project.description
                packaging = 'jar'
                url = project.url
                inceptionYear = project.inceptionYear

                scm {
                    url = 'https://github.com/FabricMC/MercuryMixin'
                    connection = 'scm:git:https://github.com/FabricMC/MercuryMixin.git'
                    developerConnection = 'scm:git:git@github.com:FabricMC/MercuryMixin.git'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/FabricMC/MercuryMixin/issues'
                }

                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://opensource.org/licenses/MPL-2.0'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'jamierocks'
                        name = 'Jamie Mansfield'
                        email = 'jmansfield@cadixdev.org'
                        url = 'https://www.jamiemansfield.me/'
                        timezone = 'Europe/London'
                    }
                }
            }
        }
    }
    repositories {
        if (ENV.MAVEN_URL) {
            maven {
                url = ENV.MAVEN_URL
                credentials {
                    username = ENV.MAVEN_USERNAME
                    password = ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}